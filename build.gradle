buildscript {
  ext.kotlinVersion = "1.3.72"
  ext.junitVersion = "5.6.2"
}

plugins {
  id "com.jfrog.bintray" version "1.8.5"
  id "maven-publish"
  id "org.jetbrains.kotlin.jvm" version "$kotlinVersion"
  id "org.jetbrains.kotlin.plugin.serialization" version "$kotlinVersion"
}

version "1.0.0"

test {
  useJUnitPlatform()

  testLogging {
    events = ["passed", "skipped", "failed"]

    // Uncomment this to show standard out and error when running tests
    // showStandardStreams = true

    // Always show test output even if nothing changed
    outputs.upToDateWhen {
      false
    }
  }
}

repositories {
  jcenter()
  mavenCentral()
}

dependencies {
  implementation "org.jetbrains.kotlin:kotlin-stdlib"
  implementation "org.jetbrains.kotlinx:kotlinx-serialization-runtime:0.20.0"

  testImplementation "org.junit.jupiter:junit-jupiter-api:$junitVersion"
  testRuntimeOnly "org.junit.jupiter:junit-jupiter-engine:$junitVersion"
}

task sourcesJar(type: Jar) {
  archiveClassifier.set("sources")
  from sourceSets.main.allSource
}

task javadocJar(type: Jar) {
  archiveClassifier.set("javadoc")
  from javadoc
}

publishing {
  publications {
    MyPublication(MavenPublication) {
      groupId "com.anthli"
      artifactId "kdiff"
      version "1.0.0"
      from components.java
      artifact sourcesJar
      artifact javadocJar
    }
  }
}

bintray {
  user = project.hasProperty("bintrayUser") ? project.property("bintrayUser") : ""
  key = project.hasProperty("bintrayKey") ? project.property("bintrayKey"): ""
  publications = ["MyPublication"]
  pkg {
    repo = "kdiff"
    name = "kdiff"
    licenses = ["MIT"]
    vcsUrl = "https://github.com/anthli/kdiff.git"
    version {
      name = "1.0.0"
      desc = "A diff library in Kotlin"
    }
  }
}